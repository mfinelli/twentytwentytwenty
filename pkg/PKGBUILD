# Maintainer: Mario Finelli <mario at finel dot li>

pkgname=twentytwentytwenty
pkgver=r17.ab61f58
pkgrel=1
pkgdesc="A reminder to look away every 20 minutes"
arch=(x86_64)
url=https://github.com/mfinelli/twentytwentytwenty
license=(GPL3)
depends=(gcc-libs)
makedepends=(cargo cargo-about imagemagick git)
source=(git+https://github.com/mfinelli/twentytwentytwenty.git
        ./twentytwentytwenty.{desktop,service})
sha256sums=('SKIP'
            '9aec1a628064b5d8671938549e6eb8eb977d937fa1dd8e87a884123b5fdc51bd'
            'e3cf69c88d16727144406e253dad209bd2dc20eee935444e0c28d5ae97cc3c50')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd  "$pkgname"
  cargo build --release --locked
  make LICENSE-THIRD-PARTY
  make pkg/twentytwentytwenty.png
}

package() {
  cd "$pkgname"
  install -Dm0755 target/release/twentytwentytwenty \
    "$pkgdir/usr/bin/twentytwentytwenty"
  install -Dm0644 "$srcdir/twentytwentytwenty.desktop" \
    "$pkgdir/usr/share/applications/twentytwentytwenty.desktop"
  install -Dm0644 pkg/twentytwentytwenty.png \
    "$pkgdir/usr/share/pixmaps/twentytwentytwenty.png"
  install -Dm0644 "$srcdir/twentytwentytwenty.service" \
    "$pkgdir/usr/lib/systemd/user/twentytwentytwenty.service"
  install -Dm0644 LICENSE-THIRD-PARTY \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-THIRD-PARTY"
}

# vim: set ts=2 sw=2 et:
